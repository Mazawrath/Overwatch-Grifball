settings
{
	lobby
	{
		Max Team 1 Players: 4
		Max Team 2 Players: 4
	}

	modes
	{
		Team Deathmatch
		{
			enabled maps
			{
				Workshop Chamber
			}
		}

		General
		{
			Allow Hero Switching: Off
			Game Mode Start: Manual
			Hero Limit: Off
			Respawn Time Scalar: 25%
			Score To Win: 7
		}
	}

	heroes
	{
		General
		{
			Genji
			{
				Damage Dealt: 500%
				Deflect: Off
				Health: 10%
				No Ammunition Requirement: On
				Primary Fire: Off
				Secondary Fire: Off
				Swift Strike: Off
				Ultimate Ability Dragonblade: Off
			}

			Torbjörn
			{
				Deploy Turret: Off
				Health: 10%
				No Ammunition Requirement: On
				Overload: Off
				Quick Melee: Off
				Ultimate Ability Molten Core: Off
				Weapons Enabled: Forge Hammer Only
			}

			enabled heroes
			{
				Genji
				Torbjörn
			}
		}
	}
}

variables
{
	global:
		0: ViewAngle
		1: PlayerSwapCooldown
		2: BombCarrier
		3: BombLocation
		4: BombPickedUp
		6: BombCountdown
		7: BombTimer
		8: BombCountdownText
		9: RespawnRequested

	player:
		1: PlayerTargeted
		2: PlayerInRange
		3: LungeEnabled
		4: DistanceToAllowLunge
		5: LungeStrength
		6: HeroSwapCooldown
		7: InDistanceOfBomb
		8: PrimaryButtonIsHeld
		9: TimeSinceHammerUsed
		10: ReloadButtonIsHeld
		11: TimeSinceReloadHeld
}

subroutines
{
	0: LungeAtPlayer
	1: ExplodeBomb
	2: StartGame
	3: DropBomb
}

rule("Create Effects")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Create Effect(All Players(All Teams), Ring, White, 0, 1, Visible To Position and Radius);
		Create Effect(All Players(All Teams), Ring, Team 1, Vector(17, 1, 0), 1, Visible To Position and Radius);
		Create Effect(All Players(All Teams), Ring, Team 2, Vector(-17.000, 1, 0), 1, Visible To Position and Radius);
		Create Effect(Filtered Array(All Players(Team 1), Compare(Current Array Element, ==, Global Variable(BombCarrier))), Light Shaft,
			Team 2, Vector(-17.000, 0, 0), 1, Visible To Position and Radius);
		Create Effect(Filtered Array(All Players(Team 2), Compare(Current Array Element, ==, Global Variable(BombCarrier))), Light Shaft,
			Team 1, Vector(17, 0, 0), 1, Visible To Position and Radius);
	}
}

rule("Ball spawns on match start")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Create Effect(All Players(All Teams), Orb, Orange, Global Variable(BombLocation), 0.001, Visible To Position and Radius);
	}
}

rule("Create Global HUD Elements")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Create HUD Text(All Players(All Teams), Null, Null, Custom String("Controls:", Null, Null, Null), Right, 1, White, White, White,
			Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Null, Custom String("Secondary Fire: Switch Weapons", Null, Null, Null), Right, 2,
			White, White, White, Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Null, Custom String("Reload: Pick up bomb", Null, Null, Null), Right, 3, White,
			White, White, Visible To and String, Default Visibility);
	}
}

rule("Create Local HUD Elements")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
	}

	actions
	{
		Create HUD Text(Filtered Array(All Players(All Teams), And(Not(Global Variable(BombPickedUp)), Player Variable(
			Current Array Element, InDistanceOfBomb))), Custom String("Hold Reload to pick up bomb", Null, Null, Null), Null, Null, Left,
			0, White, White, White, Visible To and String, Default Visibility);
	}
}

rule("Spawn team 1")
{
	event
	{
		Ongoing - Each Player;
		Team 1;
		All;
	}

	conditions
	{
		Is Alive(Event Player) == True;
		Has Spawned(Event Player) == True;
	}

	actions
	{
		Teleport(Event Player, Vector(17, 0, Random Real(-20.000, 20)));
		Set Facing(Event Player, Right, To World);
	}
}

rule("Spawn team 2")
{
	event
	{
		Ongoing - Each Player;
		Team 2;
		All;
	}

	conditions
	{
		Is Alive(Event Player) == True;
		Has Spawned(Event Player) == True;
	}

	actions
	{
		Teleport(Event Player, Vector(-17.000, 0, Random Real(-20.000, 20)));
		Set Facing(Event Player, Left, To World);
	}
}

rule("Set global variables")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Set Global Variable(ViewAngle, 15);
		Set Global Variable(PlayerSwapCooldown, 1.500);
		Set Global Variable(BombLocation, Vector(0, 0.250, 0));
		Set Global Variable(BombTimer, 3);
		Chase Global Variable At Rate(BombCountdown, 0, 1, Destination and Rate);
		Disable Built-In Game Mode Scoring;
	}
}

rule("Set player variables")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		Set Player Variable(Event Player, HeroSwapCooldown, 0);
		Chase Player Variable At Rate(Event Player, HeroSwapCooldown, 0, 1, Destination and Rate);
		Preload Hero(Event Player, Hero(Genji));
		Preload Hero(Event Player, Hero(Torbjörn));
	}
}

rule("Torbjorn - Set player variables")
{
	event
	{
		Ongoing - Each Player;
		All;
		Torbjörn;
	}

	actions
	{
		"This simulates the gravity hammer. Which has a longer cast time but more range."
		Set Player Variable(Event Player, LungeEnabled, False);
		Set Player Variable(Event Player, LungeStrength, 12);
		Set Player Variable(Event Player, DistanceToAllowLunge, 7);
		"Apparently the custom game settings forget the genjis primary fire is disabled."
		Set Primary Fire Enabled(Event Player, True);
		Set Player Variable(Event Player, TimeSinceHammerUsed, 1);
		Chase Player Variable At Rate(Event Player, TimeSinceHammerUsed, 20, 1, Destination and Rate);
		Allow Button(Event Player, Jump);
	}
}

rule("Genji - Set player variables")
{
	event
	{
		Ongoing - Each Player;
		All;
		Genji;
	}

	actions
	{
		"This simulates the energy sword. Less cast time but less range."
		Set Player Variable(Event Player, LungeEnabled, True);
		Set Player Variable(Event Player, LungeStrength, 12);
		Set Player Variable(Event Player, DistanceToAllowLunge, 5);
		Disallow Button(Event Player, Melee);
		"Apparently the custom game settings forget the genjis primary fire is disabled."
		Set Primary Fire Enabled(Event Player, False);
	}
}

rule("Torbjorn - Lock hammer after swing")
{
	event
	{
		Ongoing - Each Player;
		All;
		Torbjörn;
	}

	conditions
	{
		Is Button Held(Event Player, Primary Fire) == True;
		Player Variable(Event Player, ButtonIsHeld) == False;
	}

	actions
	{
		If(Player Variable(Event Player, PlayerInRange));
			Call Subroutine(LungeAtPlayer);
		End;
		Set Player Variable(Event Player, TimeSinceHammerUsed, 0);
		Set Player Variable(Event Player, ButtonIsHeld, True);
		Set Primary Fire Enabled(Event Player, False);
		Set Player Variable(Event Player, LungeEnabled, True);
		Wait(0.001, Ignore Condition);
		Set Player Variable(Event Player, LungeEnabled, False);
		Wait(0.250, Ignore Condition);
		Play Effect(All Players(All Teams), Debuff Impact Sound, White, Event Player, 40);
	}
}

rule("Torbjorn - Unlock hammer after swing")
{
	event
	{
		Ongoing - Each Player;
		All;
		Torbjörn;
	}

	conditions
	{
		Is Button Held(Event Player, Primary Fire) == False;
		Player Variable(Event Player, ButtonIsHeld) == True;
		Player Variable(Event Player, TimeSinceHammerUsed) >= 0.600;
	}

	actions
	{
		Set Primary Fire Enabled(Event Player, True);
		disabled Wait(0.400, Ignore Condition);
		Set Player Variable(Event Player, ButtonIsHeld, False);
	}
}

rule("Get player to target")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		"At least 1 player is within the players reticle"
		If(Compare(Count Of(Filtered Array(Filtered Array(Players in View Angle(Event Player, Opposite Team Of(Team Of(Event Player)),
			Global Variable(ViewAngle)), Compare(Distance Between(Event Player, Current Array Element), <=, Player Variable(Event Player,
			DistanceToAllowLunge))), Is Alive(Current Array Element))), >=, 1));
			Set Player Variable(Event Player, PlayerTargeted, Eye Position(First Of(Sorted Array(Filtered Array(Filtered Array(
				All Living Players(Opposite Team Of(Team Of(Event Player))), Compare(Distance Between(Event Player, Current Array Element), <=,
				Player Variable(Event Player, DistanceToAllowLunge))), Is In View Angle(Event Player, Position Of(Current Array Element),
				Global Variable(ViewAngle))), Angle Between Vectors(Facing Direction Of(Event Player), Direction Towards(Eye Position(
				Event Player), Eye Position(Current Array Element)))))));
			Set Player Variable(Event Player, PlayerInRange, True);
		Else;
			Set Player Variable(Event Player, PlayerTargeted, Vector(0, -20.000, 0));
			Set Player Variable(Event Player, PlayerInRange, False);
		End;
		Set Player Variable(Event Player, InDistanceOfBomb, Compare(Distance Between(Position Of(Event Player), Global Variable(
			BombLocation)), <=, 2));
		Wait(0.001, Ignore Condition);
		Loop;
	}
}

rule("Create target particle")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		Create Effect(Event Player, Bad Aura, Red, Player Variable(Event Player, PlayerTargeted), 1, Position and Radius);
	}
}

rule("Swap heroes on secondary fire")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Secondary Fire) == True;
		Player Variable(Event Player, HeroSwapCooldown) == 0;
	}

	actions
	{
		Set Player Variable(Event Player, HeroSwapCooldown, Global Variable(PlayerSwapCooldown));
		If(Compare(Hero Of(Event Player), ==, Hero(Genji)));
			Start Forcing Player To Be Hero(Event Player, Hero(Torbjörn));
		Else;
			Start Forcing Player To Be Hero(Event Player, Hero(Genji));
		End;
	}
}

rule("Genji - Disable double jump in the air")
{
	event
	{
		Ongoing - Each Player;
		All;
		Genji;
	}

	conditions
	{
		Is On Ground(Event Player) == False;
	}

	actions
	{
		Disallow Button(Event Player, Jump);
	}
}

rule("Genji - Enable double jump on the ground")
{
	event
	{
		Ongoing - Each Player;
		All;
		Genji;
	}

	conditions
	{
		Is On Ground(Event Player) == True;
		Is Button Held(Event Player, Jump) == False;
	}

	actions
	{
		Allow Button(Event Player, Jump);
	}
}

rule("Genji - Use melee on primary fire button press")
{
	event
	{
		Ongoing - Each Player;
		All;
		Genji;
	}

	conditions
	{
		Is Button Held(Event Player, Primary Fire) == True;
	}

	actions
	{
		Press Button(Event Player, Melee);
	}
}

rule("Genji - Delay melee")
{
	event
	{
		Ongoing - Each Player;
		All;
		Genji;
	}

	conditions
	{
		Is Button Held(Event Player, Melee) == True;
	}

	actions
	{
		"This delay is used to simulate the energy swords cast. Without this, Genji's cast is next to nothing and can kill Torb in every fight."
		Wait(0.145, Ignore Condition);
		Allow Button(Event Player, Melee);
		Start Holding Button(Event Player, Melee);
		Wait(0.100, Ignore Condition);
		Stop Holding Button(Event Player, Melee);
		Disallow Button(Event Player, Melee);
	}
}

rule("Genji - Trigger lunge")
{
	event
	{
		Ongoing - Each Player;
		All;
		Genji;
	}

	conditions
	{
		Is Button Held(Event Player, Melee) == True;
		Player Variable(Event Player, PlayerInRange) == True;
		Player Variable(Event Player, LungeEnabled) == True;
	}

	actions
	{
		Call Subroutine(LungeAtPlayer);
	}
}

disabled rule("Torbjron - Trigger lunge")
{
	event
	{
		Ongoing - Each Player;
		All;
		Torbjörn;
	}

	conditions
	{
		Is Button Held(Event Player, Primary Fire) == True;
		Player Variable(Event Player, PlayerInRange) == True;
		Player Variable(Event Player, LungeEnabled) == True;
		disabled Player Variable(Event Player, ButtonIsHeld) == False;
		disabled Is Firing Primary(Event Player) == True;
	}

	actions
	{
		Call Subroutine(LungeAtPlayer);
	}
}

rule("Lunge at player")
{
	event
	{
		Subroutine;
		LungeAtPlayer;
	}

	actions
	{
		If(Compare(Distance Between(Eye Position(Event Player), Player Variable(Event Player, PlayerTargeted)), >, 2.500));
			If(Not(Is On Ground(Event Player)));
				Apply Impulse(Event Player, Down, 1.500, To World, Cancel Contrary Motion);
			disabled Else;
				disabled Apply Impulse(Event Player, Up, 0.250, To World, Cancel Contrary Motion);
			End;
			Apply Impulse(Event Player, Direction Towards(Position Of(Event Player), Player Variable(Event Player, PlayerTargeted)), Subtract(
				Player Variable(Event Player, LungeStrength), Y Component Of(Position Of(Event Player))), To World,
				Incorporate Contrary Motion);
			Start Facing(Event Player, Direction Towards(Eye Position(Event Player), Player Variable(Event Player, PlayerTargeted)), 100,
				To World, None);
			Wait(0.250, Ignore Condition);
			Stop Facing(Event Player);
			Stop Forcing Throttle(Event Player);
		End;
	}
}

rule("// GRIFBALL RULES")
{
	event
	{
		Ongoing - Global;
	}
}

rule("Pickup bomb")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Player Variable(Event Player, InDistanceOfBomb) == True;
		Global Variable(BombPickedUp) == False;
		Is Button Held(Event Player, Reload) == True;
		Is Game In Progress == True;
	}

	actions
	{
		Small Message(All Players(All Teams), Custom String("Bomb taken", Null, Null, Null));
		Set Global Variable(BombCarrier, Event Player);
		Set Global Variable(BombPickedUp, True);
		Set Player Variable(Event Player, ReloadButtonIsHeld, True);
	}
}

rule("Reload button let go")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Reload) == False;
		Player Variable(Event Player, ReloadButtonIsHeld) == True;
	}

	actions
	{
		Set Player Variable(Event Player, ReloadButtonIsHeld, False);
	}
}

rule("Bomb follows carrier")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Global Variable(BombCarrier) != Null;
		Is Game In Progress == True;
	}

	actions
	{
		Chase Global Variable At Rate(BombLocation, World Vector Of(Multiply(Forward, 1), Global Variable(BombCarrier),
			Rotation And Translation), 100, Destination and Rate);
	}
}

rule("// DEV TOOLS")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Create HUD Text(All Players(All Teams), Null, Custom String("Created by u/Mazawrath", Null, Null, Null), Null, Left, 0, White,
			White, White, Visible To and String, Default Visibility);
	}
}

rule("Spawn Dummies - Team 1")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Create Dummy Bot(Hero(Torbjörn), Team 1, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Genji), Team 1, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Torbjörn), Team 1, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Genji), Team 1, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Torbjörn), Team 1, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Genji), Team 1, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
	}
}

rule("Spawn Dummies - Team 2")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Create Dummy Bot(Hero(Torbjörn), Team 2, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Genji), Team 2, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Torbjörn), Team 2, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Genji), Team 2, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Torbjörn), Team 2, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Genji), Team 2, -1.000, Vector(0, 0, 0), Vector(0, 0, 0));
	}
}

disabled rule("Dummies always use primary fire")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Dummy Bot(Event Player) == True;
	}

	actions
	{
		Press Button(Event Player, Primary Fire);
		Wait(0.250, Ignore Condition);
		Loop;
	}
}

rule("Bomb Reaches Site - Team 1")
{
	event
	{
		Ongoing - Each Player;
		Team 1;
		All;
	}

	conditions
	{
		Event Player == Global Variable(BombCarrier);
		Distance Between(Position Of(Event Player), Vector(-17.000, 0, 0)) <= 1;
		Is Game In Progress == True;
	}

	actions
	{
		Stop Chasing Global Variable(BombLocation);
		Set Global Variable(BombCarrier, Null);
		Set Global Variable(BombLocation, Vector(-17.000, 0.250, 0));
		Small Message(All Players(All Teams), Custom String("Bomb Armed", Null, Null, Null));
		Call Subroutine(ExplodeBomb);
	}
}

rule("Bomb Reaches Site - Team 2")
{
	event
	{
		Ongoing - Each Player;
		Team 2;
		All;
	}

	conditions
	{
		Event Player == Global Variable(BombCarrier);
		Distance Between(Position Of(Event Player), Vector(17, 0, 0)) <= 1;
		Is Game In Progress == True;
	}

	actions
	{
		Stop Chasing Global Variable(BombLocation);
		Set Global Variable(BombCarrier, Null);
		Set Global Variable(BombLocation, Vector(17, 0.250, 0));
		Small Message(All Players(All Teams), Custom String("Bomb Armed", Null, Null, Null));
		Call Subroutine(ExplodeBomb);
	}
}

rule("Bomb carrier drops bomb")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player == Global Variable(BombCarrier);
		Is Button Held(Event Player, Reload) == True;
		Is Game In Progress == True;
		Player Variable(Event Player, ReloadButtonIsHeld) == False;
	}

	actions
	{
		Call Subroutine(DropBomb);
	}
}

rule("Bomb carrier dies")
{
	event
	{
		Player Died;
		All;
		All;
	}

	conditions
	{
		Event Player == Global Variable(BombCarrier);
	}

	actions
	{
		Call Subroutine(DropBomb);
	}
}

rule("Drop bomb")
{
	event
	{
		Subroutine;
		DropBomb;
	}

	actions
	{
		Stop Chasing Global Variable(BombLocation);
		Set Global Variable(BombCarrier, Null);
		Small Message(All Players(All Teams), Custom String("Bomb Dropped", Null, Null, Null));
		Wait(0.250, Ignore Condition);
		Set Global Variable(BombPickedUp, False);
	}
}

rule("Explode bomb")
{
	event
	{
		Subroutine;
		ExplodeBomb;
	}

	actions
	{
		Set Global Variable(BombCountdown, Global Variable(BombTimer));
		Create In-World Text(All Players(All Teams), Custom String("{0}", Round To Integer(Global Variable(BombCountdown), Up), Null,
			Null), Vector(X Component Of(Global Variable(BombLocation)), Add(Y Component Of(Global Variable(BombTimer)), 1),
			Z Component Of(Global Variable(BombLocation))), 15, Clip Against Surfaces, Visible To Position and String, Red,
			Default Visibility);
		Set Global Variable(BombCountdownText, Last Text ID);
		Wait(Global Variable(BombTimer), Ignore Condition);
		Play Effect(All Players(All Teams), Buff Impact Sound, White, Global Variable(BombLocation), 200);
		Destroy In-World Text(Global Variable(BombCountdownText));
		Modify Team Score(Team Of(Event Player), 1);
		Play Effect(All Players(All Teams), Bad Explosion, Orange, Global Variable(BombLocation), 20);
		Kill(Filtered Array(All Players(All Teams), Compare(Distance Between(Position Of(Current Array Element), Global Variable(
			BombLocation)), <=, 10)), Event Player);
		Small Message(All Players(All Teams), Custom String("Bomb Detonated.", Null, Null, Null));
		Wait(1.500, Ignore Condition);
		Small Message(All Players(All Teams), Custom String("Round Over.", Null, Null, Null));
		Wait(2, Ignore Condition);
		Call Subroutine(StartGame);
	}
}

rule("StartGame")
{
	event
	{
		Subroutine;
		StartGame;
	}

	actions
	{
		Set Global Variable(BombLocation, Vector(0, 0.250, 0));
		Set Global Variable(BombCarrier, Null);
		Set Global Variable(BombPickedUp, False);
		Set Global Variable(RespawnRequested, True);
		Set Move Speed(All Players(All Teams), 0);
		Small Message(All Players(All Teams), Custom String("Griffball.", Null, Null, Null));
		Wait(1, Ignore Condition);
		Small Message(All Players(All Teams), Custom String("3", Null, Null, Null));
		Wait(1, Ignore Condition);
		Small Message(All Players(All Teams), Custom String("2", Null, Null, Null));
		Wait(1, Ignore Condition);
		Small Message(All Players(All Teams), Custom String("1", Null, Null, Null));
		Wait(1, Ignore Condition);
		Small Message(All Players(All Teams), Custom String("0", Null, Null, Null));
		Set Move Speed(All Players(All Teams), 100);
	}
}

rule("Respawn requested - Team 1")
{
	event
	{
		Ongoing - Each Player;
		Team 1;
		All;
	}

	conditions
	{
		Global Variable(RespawnRequested) == True;
	}

	actions
	{
		Set Global Variable(RespawnRequested, False);
		Teleport(Event Player, Vector(17, 0, Random Real(-20.000, 20)));
		Resurrect(Event Player);
		Set Facing(Event Player, Right, To World);
	}
}

rule("Respawn requested - Team 2")
{
	event
	{
		Ongoing - Each Player;
		Team 2;
		All;
	}

	conditions
	{
		Global Variable(RespawnRequested) == True;
	}

	actions
	{
		Set Global Variable(RespawnRequested, False);
		Teleport(Event Player, Vector(-17.000, 0, Random Real(-20.000, 20)));
		Resurrect(Event Player);
		Set Facing(Event Player, Right, To World);
	}
disabled rule("Reset bomb")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Is Game In Progress == True;
		Y Component Of(Global Variable(BombLocation)) < 0;
		Global Variable(BombPickedUp) == False;
	}

	actions
	{
		Set Global Variable(BombLocation, Vector(0, 0.250, 0));
		Set Global Variable(BombCarrier, Null);
		Set Global Variable(BombPickedUp, False);
		Small Message(All Players(All Teams), Custom String("Bomb Reset.", Null, Null, Null));
	}
}